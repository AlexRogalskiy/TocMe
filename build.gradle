buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'com.github.blueboxware.tocme' version '1.1'
}

group 'com.github.blueboxware'
version pluginVersion

apply plugin: 'kotlin'

sourceCompatibility = jvmTarget

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.vladsch.flexmark:flexmark:$flexmarkVersion"
    implementation "com.vladsch.flexmark:flexmark-ext-toc:$flexmarkVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect"

    testImplementation "org.jetbrains.spek:spek-api:$spekVersion"
    testImplementation("org.jetbrains.spek:spek-data-driven-extension:$spekVersion") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-runtime'
    }
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation gradleTestKit()

    testRuntimeOnly "org.jetbrains.spek:spek-junit-platform-engine:$spekVersion"
    // Needed for the Spek plugin to work correctly
    testImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.4"

}

compileKotlin {
    kotlinOptions.jvmTarget = jvmTarget
}
compileTestKotlin {
    kotlinOptions.jvmTarget = jvmTarget
}

gradlePlugin {
    plugins {
        TocMe {
            id = 'com.github.blueboxware.tocme'
            implementationClass = 'com.github.blueboxware.tocme.TocMePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/BlueBoxWare/TocMe'
    vcsUrl = 'https://github.com/BlueBoxWare/TocMe.git'
    description = 'Plugin to add Table of Contents to markdown documents and keeping them up to date'
    tags = ['readme', 'table of content', 'toc', 'markdown']

    plugins {
        TocMe {
            displayName = 'TocMe'
            version = pluginVersion
        }
    }
}

tocme {
    doc(file('README.md.src'))
}

task createReadme {

    inputs.file("README.md.src")
    inputs.file("versions.gradle")

    outputs.file("README.md")

    file("README.md").text = file("README.md.src").text.replaceAll("<releasedPluginVersion>", releasedPluginVersion)

    dependsOn += tasks.getByName('insertTocs')

}
